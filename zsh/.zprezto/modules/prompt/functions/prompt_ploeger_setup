#
# Martin Plöger's theme
#
# Authors:
#   Martin Plöger <martin.ploeger@web.de>
#

# Load dependencies.
pmodload 'helper'

function prompt_ploeger_pwd {
  local pwd="${PWD/#$HOME/~}"

  if [[ "$pwd" == (#m)[/~] ]]; then
    _prompt_ploeger_pwd="$MATCH"
    unset MATCH
  else
    _prompt_ploeger_pwd="${${${(@j:/:M)${(@s:/:)pwd}##.#?}:h}%/}/${pwd:t}"
  fi
}

function prompt_ploeger_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  # Format PWD.
  prompt_ploeger_pwd

  # Get Git repository information.
  if (( $+functions[git-info] )); then
    git-info
  fi
  if (( $+functions[ruby-info] )); then
    ruby-info
  fi
}

function prompt_ploeger_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook

  # Add hook before each command.
  add-zsh-hook precmd prompt_ploeger_precmd

  zstyle ':prezto:module:editor:info:completing'               format  '%F{red}…%f'
  zstyle ':prezto:module:editor:info:keymap:primary'           format  ''
  zstyle ':prezto:module:editor:info:keymap:primary:overwrite' format  '⌘'
  zstyle ':prezto:module:editor:info:keymap:alternate'         format  '⌥'
  
  zstyle ':prezto:module:git:info:action'                      format  ':%B%F{yellow}%s%f%b'
  zstyle ':prezto:module:git:info:added'                       format  '%B%F{green}✚%f%b'
  zstyle ':prezto:module:git:info:ahead'                       format  '%B%F{yellow}⬆%f%b '
  zstyle ':prezto:module:git:info:behind'                      format  '%B%F{yellow}⬇%f%b '
  zstyle ':prezto:module:git:info:branch'                      format  '%F{green}%b%f'
  zstyle ':prezto:module:git:info:commit'                      format  ':%F{green}%.7c%f'
  zstyle ':prezto:module:git:info:deleted'                     format  '%B%F{red}✖%f%b'
  zstyle ':prezto:module:git:info:modified'                    format  '%B%F{blue}✱%f%b'
  zstyle ':prezto:module:git:info:position'                    format  ':%F{red}%p%f'
  zstyle ':prezto:module:git:info:renamed'                     format  '%B%F{magenta}➜%f%b'
  zstyle ':prezto:module:git:info:stashed'                     format  '%B%F{cyan}✭%f%b'
  zstyle ':prezto:module:git:info:unmerged'                    format  '%B%F{yellow}═%f%b'
  zstyle ':prezto:module:git:info:untracked'                   format  '%B%F{white}◼%f%b'
  zstyle ':prezto:module:git:info:keys'                        format  'rprompt' "%A%B%S%a%d%m%r%U%u$(coalesce "%b" "%p" "%c")%s"
  zstyle ':prezto:module:git:info'                             verbose 'yes'
  
  zstyle ':prezto:module:ruby:info:version'                    format  '%v'

  # Define prompts.
  PROMPT='%(!.%B%F{red}#%f%b.)%F{cyan}${_prompt_ploeger_pwd}%f%(?.%F{10}.%F{09})${editor_info[keymap]}%f '
  RPROMPT='${git_info[rprompt]}%F{red}${ruby_info}%f'
  SPROMPT='zsh: correct %F{red}%R%f to %F{green}%r%f [nyae]? '
}

prompt_ploeger_setup "$@"
